plugins {
    id 'application'
}

application {
    // Define the main class for the application.
    mainClassName = 'hello.world.App'
}

repositories {
    mavenCentral()
    maven { url = "https://jitpack.io" }
}

dependencies {
    // to be removed with Pulsar 1.8 https://github.com/apache/pulsar/issues/9045
    implementation("org.apache.avro:avro") { version { strictly "1.9.+" } }
    // add a logger
    implementation "org.slf4j:slf4j-simple:1.7.+"
    // infinitic framework
    implementation "io.infinitic:infinitic-pulsar:0.7.1"
    // infinitic dashboard
    implementation "io.infinitic:infinitic-dashboard:0.7.1"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task setupPulsar(type: JavaExec) {
    group = "infinitic"
    main = "hello.world.Setup"
    classpath = sourceSets.main.runtimeClasspath
}

task startWorkflow(type: JavaExec) {
    group = "infinitic"
    main = "hello.world.Client"
    classpath = sourceSets.main.runtimeClasspath
}

task dashboard(type: JavaExec) {
    group = "infinitic"
    main = "hello.world.Dashboard"
    classpath = sourceSets["main"].runtimeClasspath
    args("infinitic.yml")
}